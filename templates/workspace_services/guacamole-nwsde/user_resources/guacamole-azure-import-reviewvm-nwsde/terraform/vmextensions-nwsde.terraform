#
# Extensions to install:
#
# + Microsoft.GuestConfiguration.ConfigurationforWindows
# + Microsoft.Azure.Security.WindowsAttestation
#


###########################################################################################
#
# Microsoft.GuestConfiguration.ConfigurationforWindows extension

resource "azurerm_virtual_machine_extension" "guest_config_extension" {
  name                       = "AzurePolicyforWindows"
  virtual_machine_id         = azurerm_windows_virtual_machine.windowsvm.id
  publisher                  = "Microsoft.GuestConfiguration"
  type                       = "ConfigurationforWindows"
  type_handler_version       = "1.0"
  automatic_upgrade_enabled  = true
  auto_upgrade_minor_version = true

  lifecycle {
    ignore_changes = [ name, type_handler_version, settings ]
  }

  depends_on = [
    azurerm_windows_virtual_machine.windowsvm,
    azurerm_virtual_machine_extension.config_script,
    azapi_update_resource.windowsvm_tags
  ]
}

#
# May be used instead of the above if we get errors with Policy attempting to provision first
# Nb. ensure you add an az login in the porter.yaml file
#
# resource "null_resource" "install_guest_config_extension" {
#   triggers = {
#     virtual_machine = azurerm_windows_virtual_machine.windowsvm.id
#   }
#
#   provisioner "local-exec" {
#     command = <<-EOT
#       az vm extension set \
#           --extension-instance-name "AzurePolicyforWindows" \
#           --name "ConfigurationforWindows" \
#           --publisher "Microsoft.GuestConfiguration" \
#           --enable-auto-upgrade true \
#           --resource-group "${azurerm_windows_virtual_machine.windowsvm.resource_group_name}" \
#           --vm-name "${azurerm_windows_virtual_machine.windowsvm.name}" \
#          2>&1 || true
# EOT
#   }
#
#   depends_on = [
#     azurerm_windows_virtual_machine.windowsvm,
#     azurerm_virtual_machine_extension.config_script,
#     azapi_update_resource.windowsvm_tags
#   ]
# }

# Enables private link communication for the Microsoft.GuestConfiguration.ConfigurationforWindows VM extension
# https://learn.microsoft.com/en-gb/azure/governance/machine-configuration/overview#communicate-over-private-link-in-azure
#
resource "azapi_update_resource" "windowsvm_tags" {
  type        = "Microsoft.Compute/virtualMachines@2023-07-01"
  resource_id = azurerm_windows_virtual_machine.windowsvm.id

  body = {
    tags = {
      "EnablePrivateNetworkGC" = "TRUE"
    }
  }

  depends_on = [
    azurerm_windows_virtual_machine.windowsvm,
    azurerm_virtual_machine_extension.config_script
  ]
}


###########################################################################################
#
# Microsoft.Azure.Security.WindowsAttestation extension
#

resource "azurerm_virtual_machine_extension" "guest_attestation_extension" {
  name                       = "GuestAttestation"
  virtual_machine_id         = azurerm_windows_virtual_machine.windowsvm.id
  publisher                  = "Microsoft.Azure.Security.WindowsAttestation"
  type                       = "GuestAttestation"
  type_handler_version       = "1.0"
  automatic_upgrade_enabled  = true
  auto_upgrade_minor_version = true

  lifecycle {
    ignore_changes = [ name, type_handler_version, settings ]
  }

  depends_on = [
    azurerm_windows_virtual_machine.windowsvm,
    azurerm_virtual_machine_extension.config_script,
    azurerm_virtual_machine_extension.guest_config_extension
  ]
}
