#
# Extensions to install:
#
# + Microsoft.GuestConfiguration.ConfigurationForLinux
# + Microsoft.Azure.Security.LinuxAttestation
#


###########################################################################################
#
# Microsoft.GuestConfiguration.ConfigurationForLinux extension

resource "azurerm_virtual_machine_extension" "guest_config_extension" {
  name                       = "AzurePolicyforLinux"
  virtual_machine_id         = azurerm_linux_virtual_machine.nexus.id
  publisher                  = "Microsoft.GuestConfiguration"
  type                       = "ConfigurationForLinux"
  type_handler_version       = "1.0"
  automatic_upgrade_enabled  = true
  auto_upgrade_minor_version = true
  tags                       = local.tre_shared_service_tags

  lifecycle {
    ignore_changes = [name, settings, type_handler_version, tags]
  }

  depends_on = [
    azurerm_linux_virtual_machine.nexus,
    azurerm_virtual_machine_extension.keyvault,
    azapi_update_resource.linuxvm_tags
  ]
}

#
# May be used instead of the above if we get errors with Policy attempting to provision first
# Nb. ensure you add an az login in the porter.yaml file
#
# resource "null_resource" "install_guest_config_extension" {
#   triggers = {
#     virtual_machine = azurerm_linux_virtual_machine.nexus.id
#   }
#
#   provisioner "local-exec" {
#     command = <<-EOT
#       az vm extension set \
#           --extension-instance-name "AzurePolicyforLinux" \
#           --name "ConfigurationForLinux" \
#           --publisher "Microsoft.GuestConfiguration" \
#           --enable-auto-upgrade true \
#           --resource-group "${azurerm_linux_virtual_machine.nexus.resource_group_name}" \
#           --vm-name "${azurerm_linux_virtual_machine.nexus.name}" \
#          2>&1 || true
# EOT
#   }
#
#   depends_on = [
#     azurerm_linux_virtual_machine.nexus,
#     azurerm_virtual_machine_extension.config_script,
#     azapi_update_resource.linuxvm_tags
#   ]
# }

# Enables private link communication for the Microsoft.GuestConfiguration.ConfigurationForLinux VM extension
# https://learn.microsoft.com/en-gb/azure/governance/machine-configuration/overview#communicate-over-private-link-in-azure
#
resource "azapi_update_resource" "linuxvm_tags" {
  type        = "Microsoft.Compute/virtualMachines@2023-07-01"
  resource_id = azurerm_linux_virtual_machine.nexus.id

  body = {
    tags = {
      "EnablePrivateNetworkGC" = "TRUE"
    }
  }

  depends_on = [
    azurerm_linux_virtual_machine.nexus,
    azurerm_virtual_machine_extension.keyvault
  ]
}


###########################################################################################
#
# Microsoft.Azure.Security.LinuxAttestation extension
#

resource "azurerm_virtual_machine_extension" "guest_attestation_extension" {
  name                       = "GuestAttestation"
  virtual_machine_id         = azurerm_linux_virtual_machine.nexus.id
  publisher                  = "Microsoft.Azure.Security.LinuxAttestation"
  type                       = "GuestAttestation"
  type_handler_version       = "1.0"
  automatic_upgrade_enabled  = true
  auto_upgrade_minor_version = true
  tags                       = local.tre_shared_service_tags


  lifecycle {
    ignore_changes = [name, settings, type_handler_version, tags]
  }

  depends_on = [
    azurerm_linux_virtual_machine.nexus,
    azurerm_virtual_machine_extension.keyvault,
    azurerm_virtual_machine_extension.guest_config_extension
  ]
}
